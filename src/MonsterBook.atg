#include <string>

COMPILER MonsterBook

double m_dResult;

CHARACTERS
digit = "0123456789".
letter = 'a'..'z'+'A'..'Z'.

TOKENS
ident = letter{digit|letter}.
number = digit{digit}['.'{digit}].

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO '\r'

IGNORE '\t' + '\r' + '\n'

PRODUCTIONS
MonsterBook = Expr<m_dResult>.
Expr<double& dVal> = (. double dV1; .) 
    Term<dV1> 
    {
        '+' (. double dV2; .) Term<dV2> (. dV1 += dV2; .)
        | 
        '-' (. double dV2; .) Term<dV2> (. dV1 -= dV2; .)
    } (. dVal = dV1; .). 
Term<double& dVal> = 
    Factor<dVal>
    { 
        '*' (. double dV2; .) Factor<dV2> (. dVal *= dV2; .)
        | 
        '/' (. double dV2; .) Factor<dV2> (. dVal /= dV2; .)
    }.
Factor<double& dVal> = 
    number (. dVal = std::stod(t->val); .)
    | '-' number (. dVal = - std::stod(t->val); .)
    | '(' Expr<dVal> ')'.

END MonsterBook.